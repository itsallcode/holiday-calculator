plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'org.sonarqube' version '3.2.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

// holidaycalculator
group 'org.itsallcode'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

test {
    if(logger.infoEnabled) {
        testLogging.showStandardStreams = true
    }
    jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

ext {
    junitVersion = '5.7.1'
	log4jVersion = '2.14.1'
    mockitoVersion = '3.9.0'
	lockbackVersion = '1.2.3'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation 'org.assertj:assertj-core:3.19.0'
	testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
	// testRuntimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$lockbackVersion"
}

license {
    // header = file('gradle/license-header.txt')
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

rootProject.tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])

def getOptionalProperty(String name) {
    if(project.hasProperty(name)) {
        return project.property(name)
    }
    logger.info("Project property '${name}' not available. Please it to ~/.gradle/gradle.properties")
    return null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
				// TBD
                name = 'FritzBox Java API'
                description = 'Java API for managing FritzBox HomeAutomation '
                url = 'https://github.com/kaklakariada/fritzbox-java-api'

                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ckunki'
                        name = 'Christoph'
                        email = 'github@kuhnke.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/itsallcode/holiday-calculator.git'
                    developerConnection = 'scm:git:https://github.com/itsallcode/holiday-calculator.git'
                    url = 'https://github.com/itsallcode/holiday-calculator'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol = false
            credentials(PasswordCredentials) {
                username = getOptionalProperty("ossrhUsername")
                password = getOptionalProperty("ossrhPassword")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusStaging {
    packageGroup = project.group
    username = getOptionalProperty("ossrhUsername")
    password = getOptionalProperty("ossrhPassword")
}
